## <summary>Policy controlling access to the Gitea webserver</summary>
## <required val="false">
##  Contains optional interfaces for controlling access to Gitea.
## </required>
#
########################################
## <summary>
##  Allow search access of Gitea lib data.
## </summary>
## <desc>
##  <p>
##  Allow search access of Gitea lib data.
##  </p>
## </desc>
## <param name="domain">
##  <summary>
##  Type to be allowed search access of Gitea lib data.
##  </summary>
## </param>
#
interface(`gitea_search_lib',`
    gen_require(`
        type gitea_var_lib_t;
    ')

    files_search_var_lib($1)
    allow $1 gitea_var_lib_t:dir search_dir_perms;
')

########################################
## <summary>
##  Allow read access to Gitea lib data.
## </summary>
## <desc>
##  <p>
##  Allow read access to Gitea lib data.
##  </p>
## </desc>
## <param name="domain">
##  <summary>
##  Type to be allowed read access to Gitea lib data.
##  </summary>
## </param>
#
interface(`gitea_read_lib_files',`
    gen_require(`
        type gitea_var_lib_t;
    ')

    allow $1 gitea_var_lib_t:file read_file_perms;
')

########################################
## <summary>
##  Allow connecting to the Gitea port.
## </summary>
## <desc>
##  <p>
##  Allow connecting to the Gitea port.
##  </p>
## </desc>
## <param name="domain">
##  <summary>
##  Type to be allowed to connect to the Gitea port.
##  </summary>
## </param>
#
interface(`gitea_tcp_connect_port',`
    gen_require(`
        type gitea_port_t;
    ')

    allow $1 gitea_port_t:tcp_socket name_connect;
')

########################################
## <summary>
##  All of the rules required to
##  administrate a Gitea environment.
## </summary>
## <param name="domain">
##  <summary>
##  Domain allowed access.
##  </summary>
## </param>
## <param name="role">
##  <summary>
##  Role allowed access.
##  </summary>
## </param>
## <rolecap/>
#
interface(`gitea_admin',`
    gen_require(`
        type gitea_t, gitea_initrc_exec_t, gitea_log_t;
        type gitea_runtime_t;
    ')

    init_startstop_service($1, $2, gitea_t, gitea_initrc_exec_t)

    allow $1 gitea_t:process { ptrace signal_perms };
    ps_process_pattern($1, gitea_t)

    logging_search_logs($1)
    admin_pattern($1, gitea_log_t)

    files_search_pids($1)
    admin_pattern($1, gitea_runtime_t)
')
